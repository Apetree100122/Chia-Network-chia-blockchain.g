; This is a PARENT_MORPHER for use with covenant_layer.clsp

; Is is used to prove that the parent was a singleton -> ownership_layer (w/ covenant_layer in TP) puzzle stack
(mod
  (
    ; curried first - a bunch of info to create a singleton -> ownership layer puzzle stack
    COVENANT_MOD_HASH
    OWNERSHIP_LAYER_MOD_HASH
    ADAPTER_MOD_HASH
    COVENANT_INITIAL_PUZZLE_HASH_HASH
    SINGLETON_STRUCT
    TP_HASH
    ; curried second
    SELF_HASH  ; tree hash of all the other hashes above
    previous_inner_puzhash  ; Truth
    previous_metadata_hash
  )

  (include curry.clib)

  (curry_hashes (f SINGLETON_STRUCT)
    (sha256 2
      (sha256 1 (f SINGLETON_STRUCT))
      (sha256 2
        (sha256 1 (f (r SINGLETON_STRUCT)))
        (sha256 1 (r (r SINGLETON_STRUCT)))
      )
    )
    (curry_hashes OWNERSHIP_LAYER_MOD_HASH
      (sha256 1 OWNERSHIP_LAYER_MOD_HASH)
      previous_metadata_hash
      (curry_hashes ADAPTER_MOD_HASH
        (curry_hashes COVENANT_MOD_HASH
          COVENANT_INITIAL_PUZZLE_HASH_HASH
          (curry_hashes SELF_HASH
            (sha256 1 SELF_HASH)
          )
          TP_HASH
        )
      )
      previous_inner_puzhash
    )
  )
)