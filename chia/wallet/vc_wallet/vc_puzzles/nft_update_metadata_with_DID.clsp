; This is a TRANSFER_PROGRAM for use with nft_ownership_layer.clsp

; Given a DID's singleton struct, assert its announcement of any changes. If no DID's innerpuz is provided, then just
; return the same metadata and transfer program with no conditions
(mod
  (
    PROVIDER_SINGLETON_STRUCT
    Current_Owner  ; Truth
    conditions  ; Truth
    (
      provider_innerpuzhash
      my_coin_id
      new_metadata
      new_transfer_program
    )
  )

  (include condition_codes.clvm)
  (include curry.clib)
  (include sha256tree.clib)

  (defun-inline create_did_puzhash
    (
        PROVIDER_SINGLETON_STRUCT
        provider_innerpuzhash
    )

    (curry_hashes_inline (f PROVIDER_SINGLETON_STRUCT)
      (sha256tree PROVIDER_SINGLETON_STRUCT)
      provider_innerpuzhash
    )
  )

  ; Returning (new_metadata new_transfer_program conditions)
  (if provider_innerpuzhash
    (list
      new_metadata
      (i new_transfer_program new_transfer_program ())
      (list
        (list ASSERT_MY_COIN_ID my_coin_id)
        (list ASSERT_PUZZLE_ANNOUNCEMENT
          (sha256
            (create_did_puzhash PROVIDER_SINGLETON_STRUCT provider_innerpuzhash)
            (sha256
              my_coin_id
              (sha256tree new_metadata)
              (sha256tree new_transfer_program)
            )
          )
        )
      )
    )
    (list Current_Owner () ())
  )
)